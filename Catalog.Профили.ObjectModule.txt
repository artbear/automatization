
#Область ПрограммныйИнтерфейс

Процедура ОбновитьРеквизиты() Экспорт
	
	МетаданныеМодуля = ПолучитьОбработкуПоТипуПрофиля()
							.Создать()
							.Метаданные();
	Для каждого Реквизит из МетаданныеМодуля.Реквизиты Цикл
		
		НайденнаяСтрока = РеквизитыПрофиля.Найти(Реквизит.Имя, "Реквизит");
		Если НЕ НайденнаяСтрока = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		РеквизитыПрофиля.Добавить().Реквизит = Реквизит.Имя;
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыполнитьТекущийСценарий() Экспорт
	
	ТекущийМодуль = ПолучитьОбработкуПоТипуПрофиля().Создать();
	ЗаполнитьТекущуюОбработкуИзПрофиля(ТекущийМодуль);
	ТекущийМодуль.ВыполнитьСценарий();
	
КонецПроцедуры

Функция ПолучитьОбработкуПоТипуПрофиля() Экспорт
	
	ИдентификаторПрофиля = ИдентификаторПеречисленияПоСсылке(ТипПрофиля);
	Возврат Обработки["ТипПрофиля_" + ИдентификаторПрофиля];
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий



#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИдентификаторПеречисленияПоСсылке(ЗначениеПеречисления) 
	
	ИмяПеречисления = ЗначениеПеречисления.Метаданные().Имя;
	ИндексЗначенияПеречисления = Перечисления[ИмяПеречисления].Индекс(ЗначениеПеречисления);
	Возврат Метаданные.Перечисления[ИмяПеречисления].ЗначенияПеречисления[ИндексЗначенияПеречисления].Имя;

КонецФункции

Процедура ЗаполнитьТекущуюОбработкуИзПрофиля(ТекущаяОбработка)
	
	// шапка
	СтруктураПараметров = Новый Структура;
	Для каждого Строка из РеквизитыПрофиля Цикл
		ИмяРеквизита = Строка.Реквизит;
		СтруктураПараметров.Вставить(ИмяРеквизита, Строка.Значение.ЗначениеПараметра);
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(ТекущаяОбработка, СтруктураПараметров);
	
	// табличные части
	Для каждого ТабличнаяЧастьОбработки из ТекущаяОбработка.Метаданные().ТабличныеЧасти Цикл
		ИмяТЧ = ТабличнаяЧастьОбработки.Имя;
		Если Метаданные().ТабличныеЧасти.Найти(ИмяТЧ) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущаяОбработка[ИмяТЧ].Загрузить(ЭтотОбъект[ИмяТЧ].Выгрузить());
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти




