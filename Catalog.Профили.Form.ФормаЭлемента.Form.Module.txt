
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УправлениеЭлементамиФормы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипПрофиляПриИзменении(Элемент)
	УправлениеЭлементамиФормы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_РеквизитыПрофиля

&НаКлиенте
Процедура РеквизитыПрофиляПараметрНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.Родитель.ТекущиеДанные;
	ТипПараметра = ПолучитьТипПараметра(ТекущиеДанные.Реквизит);
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("РежимВыбора", Истина);
	
	Если ЗначениеЗаполнено(ТипПараметра) Тогда
		ЗначениеОтбора = Новый Структура;
		ЗначениеОтбора.Вставить("ТипПараметра", ТипПараметра);
		ПараметрыВыбора.Вставить("Отбор", ЗначениеОтбора);
	КонецЕсли;
	ОткрытьФорму("Справочник.ПараметрыШагов.ФормаВыбора", ПараметрыВыбора, Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьРеквизиты(Команда)
	ОбновитьРеквизитыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьТекущийСценарий(Команда)
	ВыполнитьТекущийСценарийНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеЭлементамиФормы()
	Если Объект.ТипПрофиля = Перечисления.ТипыПрофилей.НаборПрофилей Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаНаборПрофилей;
	Иначе
		Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаРеквизитыПрофиля;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьРеквизитыНаСервере()
	ЭлементОбъект = РеквизитФормыВЗначение("Объект");
	ЭлементОбъект.ОбновитьРеквизиты();
	ЗначениеВРеквизитФормы(ЭлементОбъект, "Объект");
КонецПроцедуры

&НаСервере
Процедура ВыполнитьТекущийСценарийНаСервере()
	ПараметрыСеанса.ЛогПоследнегоСценария = "";
	ЭлементОбъект = РеквизитФормыВЗначение("Объект");
	ЭлементОбъект.ВыполнитьТекущийСценарий();
КонецПроцедуры

&НаСервере
Функция ПолучитьТипПараметра(ИмяПараметра)
	ЭлементОбъект = РеквизитФормыВЗначение("Объект");
	ТекущаяОбработка = ЭлементОбъект.ПолучитьОбработкуПоТипуПрофиля().Создать();
	Возврат ТекущаяОбработка.ТипПараметра(ИмяПараметра);
КонецФункции


#КонецОбласти

