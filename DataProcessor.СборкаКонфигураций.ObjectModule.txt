Перем мДеревоКонфигурацииТаблица;
Перем мДеревоКонфигурацииDOM;
Перем мРазделительТипаИВида;

Процедура ВыполнитьСборкуКонфигурации() Экспорт
	
	Инициализировать();
	
	ЗаполнитьДеревоКонфигурацииТаблицу();
	
	ЗаполнитьДеревоКонфигурацииDOM();
	
	СоздатьФайлыСборки();
	
	УдалитьРеквизитыВЗаглушках();
	
	СоздатьПодсистемы();
	
КонецПроцедуры

Процедура Инициализировать()
	
	мДеревоКонфигурацииТаблица = Новый ТаблицаЗначений;
	мДеревоКонфигурацииТаблица.Колонки.Добавить("ИмяОбъектаКонфигурации", Новый ОписаниеТипов("Строка"));
	мДеревоКонфигурацииТаблица.Колонки.Добавить("Тип", Новый ОписаниеТипов("Строка"));
	мДеревоКонфигурацииТаблица.Колонки.Добавить("ЭтоЗаглушка", Новый ОписаниеТипов("Булево"));
	
	КаталогИсходныхФайлов = ПутьККаталогуСоСлешем(КаталогИсходныхФайлов);
	ФайлДереваИсходнойКонфигурации = ПутьККопииФайла(КаталогИсходныхФайлов + "Configuration.xml");
	мДеревоКонфигурацииDOM = ПрочитатьDOMИзФайла(ФайлДереваИсходнойКонфигурации);
	
	Файл = Новый Файл(КаталогИсходныхФайлов + "Languages" + ПолучитьРазделительПути());
	Если Файл.Существует() Тогда
		мРазделительТипаИВида = "s"+ПолучитьРазделительПути();
	Иначе
		мРазделительТипаИВида = ".";
	КонецЕсли;
	
	ОчиститьДочерниеУзлыDOMОбъекта(мДеревоКонфигурацииDOM, "ChildObjects");
	
	КаталогСборки = ПутьККаталогуСоСлешем(КаталогСборки);
	ФайлДереваДляСборки = КаталогСборки + "Configuration.xml";
	ЗаписатьDOMОбъектВФайл(мДеревоКонфигурацииDOM, ФайлДереваДляСборки);
	
КонецПроцедуры


Процедура ЗаполнитьДеревоКонфигурацииТаблицу()
	
	ЗаполнитьДеревоКонфигурации_ОсновныеОбъектыСборки();
	ЗаполнитьДеревоКонфигурации_Заглушки_xml();
	ЗаполнитьДеревоКонфигурации_Заглушки_Form();
	ЗаполнитьДеревоКонфигураци_ЗаглушкиЗаглушек_xml();
	
КонецПроцедуры

Процедура ЗаполнитьДеревоКонфигурации_ОсновныеОбъектыСборки()
	
	КопияФайлаСоСпискомОбъектов = ПутьККопииФайла(ФайлОписанияКонфигурации);
	
	Если НРег(ПолучитьРасширениеФайла(КопияФайлаСоСпискомОбъектов)) = ".xml" Тогда
		ЗаполнитьДеревоКонфигурации_ОсновныеОбъектыСборки_xml(КопияФайлаСоСпискомОбъектов);
	Иначе
		ЗаполнитьДеревоКонфигурации_ОсновныеОбъектыСборки_ИзТабличногоДокумента(КопияФайлаСоСпискомОбъектов);
	КонецЕсли;
КонецПроцедуры
Процедура ЗаполнитьДеревоКонфигурации_ОсновныеОбъектыСборки_ИзТабличногоДокумента(ПутьКФайлу)
	СписокЗагружаемыхОбъектов = Новый ТабличныйДокумент;
	СписокЗагружаемыхОбъектов.Прочитать(ПутьКФайлу);
	
	Для сч = 2 по СписокЗагружаемыхОбъектов.ВысотаТаблицы Цикл
		ТипОбъекта = СписокЗагружаемыхОбъектов.Область(сч, 1).Текст;
		ВидОбъекта = СписокЗагружаемыхОбъектов.Область(сч, 2).Текст;
		Если ПустаяСтрока(ТипОбъекта) или ПустаяСтрока(ВидОбъекта) Тогда
			Продолжить;
		КонецЕсли;
		
		ДобавитьСтрокуВДеревоКонфигурацииТаблицу(ТипОбъекта + "." + ВидОбъекта, Ложь);
		
	КонецЦикла;
	
КонецПроцедуры
Процедура ЗаполнитьДеревоКонфигурации_ОсновныеОбъектыСборки_xml(ПутьКФайлу)
	DOMОбъект = ПрочитатьDOMИзФайла(ПутьКФайлу);
	НайденныеУзлы = DOMОбъект.ПолучитьЭлементыПоИмени("xr:Item");
	Для Каждого Узел Из НайденныеУзлы Цикл
		Тип_Вид_Элемента = Узел.ТекстовоеСодержимое;
		
		ДобавитьСтрокуВДеревоКонфигурацииТаблицу(Тип_Вид_Элемента, Ложь);
		
	КонецЦикла;
КонецПроцедуры
Функция ПолучитьСловоДоРазделителя(Строка, Разделитель)
	Возврат  Лев(Строка, Найти(Строка,Разделитель)-СтрДлина(Разделитель));
КонецФункции
Процедура ДобавитьСтрокуВДеревоКонфигурацииТаблицу(ИмяОбъекта, ЭтоЗаглушка)
	Если ПустаяСтрока(ИмяОбъекта)Тогда
		Возврат;
	КонецЕсли;
	НоваяСтрока = мДеревоКонфигурацииТаблица.Добавить();
	НоваяСтрока.ИмяОбъектаКонфигурации = ИмяОбъекта;
	НоваяСтрока.ЭтоЗаглушка = ЭтоЗаглушка;
	НоваяСтрока.Тип = ПолучитьСловоДоРазделителя(ИмяОбъекта, ".");
КонецПроцедуры


Процедура ЗаполнитьДеревоКонфигурации_Заглушки_xml()
	мОбъекты = ПолучитьУпомянутыеОбъектыВФайле_xml(КаталогСборки + "Configuration.xml");
	Для каждого ИмяОбъекта из мОбъекты Цикл
		Если НЕ мДеревоКонфигурацииТаблица.Найти(ИмяОбъекта, "ИмяОбъектаКонфигурации") = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДобавитьСтрокуВДеревоКонфигурацииТаблицу(ИмяОбъекта, Истина);
	КонецЦикла;
	
	Отбор = Новый Структура("ЭтоЗаглушка", Ложь);
	мНайденныеСтроки = мДеревоКонфигурацииТаблица.НайтиСтроки(Отбор);
	
	Для Каждого Строка из мНайденныеСтроки Цикл
		мНайденныеФайлы = НайтиФайлы(КаталогИсходныхФайлов, Строка.ИмяОбъектаКонфигурации+"*.xml");
		Для каждого НайденныйФайл из мНайденныеФайлы Цикл
			мОбъекты = ПолучитьУпомянутыеОбъектыВФайле_xml(НайденныйФайл.ПолноеИмя);
			Для каждого ИмяОбъекта из мОбъекты Цикл
				Если НЕ мДеревоКонфигурацииТаблица.Найти(ИмяОбъекта, "ИмяОбъектаКонфигурации") = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				ДобавитьСтрокуВДеревоКонфигурацииТаблицу(ИмяОбъекта, Истина);
				
			КонецЦикла;
			
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры
Функция ПолучитьУпомянутыеОбъектыВФайле_xml(ПутьКФайлу)
	Состояние("Поиск связанных объектов для " + ПутьКФайлу);
	мУпомянутыеОбъекты = Новый Массив;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ПутьКФайлу);
	//Текст = ТекстовыйДокумент.ПолучитьТекст();
	
	мШаблоныПоиска = Новый Массив;
	мШаблоныПоиска.Добавить("Interface");
	мШаблоныПоиска.Добавить("Style");
	мШаблоныПоиска.Добавить("Language");
	мШаблоныПоиска.Добавить("CommonForm");
	мШаблоныПоиска.Добавить("Catalog");
	мШаблоныПоиска.Добавить("AccumulationRegister");
	мШаблоныПоиска.Добавить("InformationRegister");
	мШаблоныПоиска.Добавить("AccountingRegister");
	мШаблоныПоиска.Добавить("ChartOfAccounts");
	мШаблоныПоиска.Добавить("ChartOfCharacteristicTypes");
	мШаблоныПоиска.Добавить("Role");
	мШаблоныПоиска.Добавить("Task");
	мШаблоныПоиска.Добавить("SessionParameter");
	мШаблоныПоиска.Добавить("SettingsStorage");
	мШаблоныПоиска.Добавить("Document");
	мШаблоныПоиска.Добавить("CommonPicture");
	мШаблоныПоиска.Добавить("Report");
	мШаблоныПоиска.Добавить("Enum");
	мШаблоныПоиска.Добавить("CommandGroup");
	мШаблоныПоиска.Добавить("StyleItem");
	мШаблоныПоиска.Добавить("DataProcessor");
	мШаблоныПоиска.Добавить("BusinessProcess");

	ИсходныйТекст = ТекстовыйДокумент.ПолучитьТекст();
	Для каждого ШаблонПоиска из мШаблоныПоиска Цикл
		Текст = ИсходныйТекст;
		Подстрока = ">" + ШаблонПоиска + ".";
		ПозицияВСтроке = Найти(Текст, Подстрока);
		Пока ПозицияВСтроке Цикл
			Текст = Сред(Текст, ПозицияВСтроке + 1);
			УпомянутыйОбъект = Лев(Текст, Найти(Текст, "<")-1);
			Если СтрЧислоВхождений(УпомянутыйОбъект, ".") = 1 Тогда
				мУпомянутыеОбъекты.Добавить(УпомянутыйОбъект);
			ИначеЕсли СтрЧислоВхождений(УпомянутыйОбъект, ".") > 1 Тогда
				УпомянутыйОбъект = СтрЗаменить(УпомянутыйОбъект, ".", Символы.ПС);
				мУпомянутыеОбъекты.Добавить(
						СтрПолучитьСтроку(УпомянутыйОбъект, 1)
						+"."
						+СтрПолучитьСтроку(УпомянутыйОбъект, 2));
			КонецЕсли;
			
			ПозицияВСтроке = Найти(Текст, Подстрока);
		КонецЦикла;
		
	КонецЦикла;
	
	
	Текст = ИсходныйТекст;
	Текст = СтрЗаменить(Текст, "d4p1:", "cfg:");
	Текст = СтрЗаменить(Текст, "cfg:Characteristic.", "cfg:ChartOfCharacteristicTypes.");
	Текст = СтрЗаменить(Текст, "cfg:InformationRegisterRecordManager.", "cfg:InformationRegister.");
	Текст = СтрЗаменить(Текст, "cfg:DocumentRef<", "<");
	Текст = СтрЗаменить(Текст, "cfg:CatalogRef<", "<");
	Текст = СтрЗаменить(Текст, "cfg:DynamicList<", "<");
	Текст = СтрЗаменить(Текст, "cfg:Filter<", "<");
	Текст = СтрЗаменить(Текст, "cfg:ReportBuilder<", "<");
	Текст = СтрЗаменить(Текст, "cfg:AnyRef<", "<");
	Подстрока = ">cfg:";
	ПозицияВСтроке = Найти(Текст, Подстрока);
	Пока ПозицияВСтроке Цикл
		Текст = Сред(Текст, ПозицияВСтроке + 1);
		ИмяОбъекта = Лев(Текст, Найти(Текст, "<")-1);
		ИмяОбъекта = СтрЗаменить(ИмяОбъекта, "cfg:", "");
		ИмяОбъекта = СтрЗаменить(ИмяОбъекта, "Ref.", ".");
		ИмяОбъекта = СтрЗаменить(ИмяОбъекта, "Object.", ".");
		мУпомянутыеОбъекты.Добавить(ИмяОбъекта);
		
		ПозицияВСтроке = Найти(Текст, Подстрока);
	КонецЦикла;
	
	Текст = ИсходныйТекст;
	Подстрока = "<xr:Value name=""";
	ПозицияВСтроке = Найти(Текст, Подстрока);
	Пока ПозицияВСтроке Цикл
		Текст = Сред(Текст, ПозицияВСтроке + СтрДлина(Подстрока));
		ИмяОбъекта = Лев(Текст, Найти(Текст, """>")-1);
		мУпомянутыеОбъекты.Добавить(ИмяОбъекта);
		
		ПозицияВСтроке = Найти(Текст, Подстрока);
	КонецЦикла;
	
	
	Возврат мУпомянутыеОбъекты;
КонецФункции

Процедура ЗаполнитьДеревоКонфигураци_ЗаглушкиЗаглушек_xml()
	
	ЗаполнитьЗаглушкиДляПланаСчетов();
	ЗаполнитьЗаглушкиДляРегистров();
	
КонецПроцедуры
Процедура ЗаполнитьЗаглушкиДляПланаСчетов()
	Состояние("Заполнение заглушек планов счетов");
	Отбор = Новый Структура;
	Отбор.Вставить("ЭтоЗаглушка", Истина);
	Отбор.Вставить("Тип","ChartOfAccounts");
	НайденныеСтроки = мДеревоКонфигурацииТаблица.НайтиСтроки(Отбор);
	Для Каждого Строка Из НайденныеСтроки Цикл
		Если Найти(Строка.ИмяОбъектаКонфигурации, "ChartOfAccounts.") = 1 Тогда
			ИмяУзла = "<ExtDimensionTypes>";
		Иначе
			Продолжить;
		КонецЕсли;
		
		мНайденныеФайлы = НайтиФайлы(КаталогИсходныхФайлов, Строка.ИмяОбъектаКонфигурации+".xml");
		ПутьКФайлу = мНайденныеФайлы[0].ПолноеИмя;
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.Прочитать(ПутьКФайлу);
		Текст = ТекстовыйДокумент.ПолучитьТекст();
		Поз = Найти(Текст, ИмяУзла);
		Если Поз = 0 Тогда
			Продолжить;
		КонецЕсли;
		Текст = Сред(Текст, Поз+СтрДлина(ИмяУзла));
		ИмяОбъекта = ПолучитьСловоДоРазделителя(Текст, "<");
		ДобавитьСтрокуВДеревоКонфигурацииТаблицу(ИмяОбъекта, Истина);
	КонецЦикла;
КонецПроцедуры
Процедура ЗаполнитьЗаглушкиДляРегистров()
	
	ДокументыИРегистраторы = ПолучитьТаблицуПересеченияДокументовИРегистров();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПОДСТРОКА(т.Документ, 1, 150) КАК Документ,
	|	ПОДСТРОКА(т.Регистр, 1, 150) КАК Регистр
	|ПОМЕСТИТЬ втДокументыИРегистраторы
	|ИЗ
	|	&т КАК т
	|ГДЕ
	|	ПОДСТРОКА(т.Регистр, 1, 150) В (&УжеДобавленныеОбъекты)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	т.Регистр,
	|	МАКСИМУМ(т.Документ) КАК Документ
	|ИЗ
	|	втДокументыИРегистраторы КАК т
	|ГДЕ
	|	НЕ т.Регистр В
	|				(ВЫБРАТЬ
	|					р.Регистр
	|				ИЗ
	|					втДокументыИРегистраторы КАК р
	|				ГДЕ
	|					р.Документ В (&УжеДобавленныеОбъекты))
	|
	|СГРУППИРОВАТЬ ПО
	|	т.Регистр");
	Запрос.УстановитьПараметр("УжеДобавленныеОбъекты", мДеревоКонфигурацииТаблица.ВыгрузитьКолонку("ИмяОбъектаКонфигурации"));
	Запрос.УстановитьПараметр("т", ДокументыИРегистраторы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДобавитьСтрокуВДеревоКонфигурацииТаблицу(Выборка.Документ, Истина);
	КонецЦикла;
	
КонецПроцедуры
Функция ПолучитьТаблицуПересеченияДокументовИРегистров()
	РегистрыИРегистраторы = Новый ТаблицаЗначений;
	РегистрыИРегистраторы.Колонки.Добавить("Регистр", Новый ОписаниеТипов("Строка"));
	РегистрыИРегистраторы.Колонки.Добавить("Документ", Новый ОписаниеТипов("Строка"));
	
	RegExp = Новый COMОбъект("VBScript.RegExp");
	RegExp.Global = Истина;
	RegExp.MultiLine = Истина;
	
	
	НайденныеФайлы = НайтиФайлы(КаталогИсходныхФайлов, "Document.*.xml");
	СчетчикСостояния = 0;
	Для Каждого Файл Из НайденныеФайлы Цикл
		СчетчикСостояния=СчетчикСостояния+1;
		Если СчетчикСостояния%20 = 0 Тогда
			Состояние("Подготовка таблицы пересечения документов и регистров: " + СчетчикСостояния + "/" + НайденныеФайлы.Количество());
		КонецЕсли;
		Если СтрЧислоВхождений(Файл.ПолноеИмя, ".") <> 2 Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.Прочитать(Файл.ПолноеИмя);
		Текст = ТекстовыйДокумент.ПолучитьТекст();
		
		RegExp.Pattern = "(\<RegisterRecords>)[\S|\n| |\t|\<|\>|\:|\""|\.|\/|\=]*(?=\</RegisterRecords>)";
		Matches = RegExp.Execute(Текст);
		Если Matches.Count = 0 Тогда
			Продолжить;
		КонецЕсли;
	
		Текст = Matches.Item(0).Value;
		Текст = СтрЗаменить(Текст, "<RegisterRecords>", Символы.ПС);
		Текст = СтрЗаменить(Текст, "</RegisterRecords>", Символы.ПС);
		Текст = СтрЗаменить(Текст, "<xr:Item xsi:type=""xr:MDObjectRef"">", Символы.ПС);
		Текст = СтрЗаменить(Текст, "</xr:Item>", Символы.ПС);
		
		Для сч = 1 По СтрЧислоСтрок(Текст) Цикл
			Значение = СтрПолучитьСтроку(Текст, сч);
			Значение = СокрЛП(Значение);
			Если ПустаяСтрока(Значение) Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = РегистрыИРегистраторы.Добавить();
			НоваяСтрока.Регистр =  Значение;
			НоваяСтрока.Документ = Файл.ИмяБезРасширения;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат РегистрыИРегистраторы;
КонецФункции





Процедура ЗаполнитьДеревоКонфигурации_Заглушки_Form()
	
	ТаблицаГуидовОбъектовКонфигурации = ПолучитьГуидыОбъектовКонфигурации();
	
	Отбор = Новый Структура("ЭтоЗаглушка", Ложь);
	мНайденныеСтроки = мДеревоКонфигурацииТаблица.НайтиСтроки(Отбор);
	
	Для Каждого Строка из мНайденныеСтроки Цикл
		мНайденныеФайлы = НайтиФайлы(КаталогИсходныхФайлов, Строка.ИмяОбъектаКонфигурации+"*.Form");
		Для каждого НайденныйФайл из мНайденныеФайлы Цикл
			мОбъекты = ПолучитьУпомянутыеОбъектыВФайле_Form(НайденныйФайл.ПолноеИмя, ТаблицаГуидовОбъектовКонфигурации);
			Для каждого ИмяОбъекта из мОбъекты Цикл
				Если НЕ мДеревоКонфигурацииТаблица.Найти(ИмяОбъекта, "ИмяОбъектаКонфигурации") = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				ДобавитьСтрокуВДеревоКонфигурацииТаблицу(ИмяОбъекта, Истина);
				
			КонецЦикла;
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры
Функция ПолучитьГуидыОбъектовКонфигурации()
	ТаблицаГуидов = Новый ТаблицаЗначений;
	ТаблицаГуидов.Колонки.Добавить("Гуид");
	ТаблицаГуидов.Колонки.Добавить("ИмяОбъектаКонфигурации");
	
	КопияФайлаОписанияКонфигурации = ПутьККопииФайла(КаталогИсходныхФайлов + "Configuration.xml");
	ОбъектDOM = ПрочитатьDOMИзФайла(КопияФайлаОписанияКонфигурации);
	
	ОбъектыКонфигурации = Новый Массив;
	УзлыОбъектов = ОбъектDOM.ПолучитьЭлементыПоИмени("ChildObjects")[0].ДочерниеУзлы;
	Для каждого Узел из УзлыОбъектов Цикл
		
		ИмяОбъектаКонфигурации = Узел.ИмяЭлемента + мРазделительТипаИВида + Узел.ТекстовоеСодержимое;
		ОбъектыКонфигурации.Добавить(ИмяОбъектаКонфигурации);
		
	КонецЦикла;
	
	RegExp = Новый COMОбъект("VBScript.RegExp");
	RegExp.Global = Истина;
	RegExp.MultiLine = Истина;
	
	RegExp.Pattern = """\S{36}""";
	
	
	СчетчикДляВыводаСостояния = 0;
	Для каждого ИмяОбъектаКонфигурации из ОбъектыКонфигурации Цикл
		СчетчикДляВыводаСостояния = СчетчикДляВыводаСостояния+1;
		Если СчетчикДляВыводаСостояния%50 = 0 Тогда
			Состояние("Построение таблицы гуидов: " + СчетчикДляВыводаСостояния + "/" + ОбъектыКонфигурации.Количество());
		КонецЕсли;
		
		ИмяФайла = КаталогИсходныхФайлов + ИмяОбъектаКонфигурации + ".xml";
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.Прочитать(ИмяФайла);
		Текст = ТекстовыйДокумент.ПолучитьТекст();
		
		Matches = RegExp.Execute(Текст);
		
		Для Счетчик = 0 По Matches.Count - 1 Цикл 
			ГуидОбъекта = Matches.Item(Счетчик).Value;
			ГуидОбъекта = СтрЗаменить(ГуидОбъекта, """", "");
			
			НоваяСтрока = ТаблицаГуидов.Добавить();
			НоваяСтрока.Гуид = ГуидОбъекта;
			НоваяСтрока.ИмяОбъектаКонфигурации = ИмяОбъектаКонфигурации;
		КонецЦикла;
		
	КонецЦикла;
	
	
	ТаблицаГуидов.Индексы.Добавить("Гуид");
	Возврат ТаблицаГуидов;
	
КонецФункции



Функция ПолучитьУпомянутыеОбъектыВФайле_Form(ПутьКФайлу, ТаблицаГуидовОбъектов)
	Состояние("Поиск связанных объектов для " + ПутьКФайлу);
	мУпомянутыеОбъекты = Новый Массив;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ПутьКФайлу);
	ИсходныйТекст = ТекстовыйДокумент.ПолучитьТекст();
	
	Текст = ИсходныйТекст;
	Текст = СтрЗаменить(Текст, ",", Символы.ПС);
	Текст = СтрЗаменить(Текст, "{", Символы.ПС);
	Текст = СтрЗаменить(Текст, "}", Символы.ПС);
	
	RegExp = Новый COMОбъект("VBScript.RegExp");
	RegExp.Global = Истина;
	RegExp.MultiLine = Истина;
	
	RegExp.Pattern = "^\S{36}$";
	Matches = RegExp.Execute(Текст);
	
	Для Счетчик = 0 По Matches.Count - 1 Цикл 
		ГуидОбъекта = Matches.Item(Счетчик).Value;
		
		Отбор = Новый Структура("Гуид", ГуидОбъекта);
		НайденныеСтроки = ТаблицаГуидовОбъектов.НайтиСтроки(Отбор);
		Для каждого Строка из НайденныеСтроки Цикл
			мУпомянутыеОбъекты.Добавить(Строка.ИмяОбъектаКонфигурации);
		КонецЦикла;
	КонецЦикла;
	
	
	Возврат мУпомянутыеОбъекты;
КонецФункции



Процедура ЗаполнитьДеревоКонфигурацииDOM()
	
	Отбор = Новый Структура("ЭтоЗаглушка", Ложь);
	ДополнитьДеревоКонфигурацииDOM_ПоОтобору(Отбор);
	
	КорневойУзелОбъектов = мДеревоКонфигурацииDOM.ПолучитьЭлементыПоИмени("ChildObjects")[0];
	НовыйУзел = мДеревоКонфигурацииDOM.СоздатьКомментарий("ЗАГЛУШКИ");
	КорневойУзелОбъектов.ДобавитьДочерний(НовыйУзел);
	
	Отбор = Новый Структура("ЭтоЗаглушка", Истина);
	ДополнитьДеревоКонфигурацииDOM_ПоОтобору(Отбор);
	
КонецПроцедуры
Процедура ДополнитьДеревоКонфигурацииDOM_ПоОтобору(Отбор)
	
	КорневойУзелОбъектов = мДеревоКонфигурацииDOM.ПолучитьЭлементыПоИмени("ChildObjects")[0];
	
	мНайденныеСтроки = мДеревоКонфигурацииТаблица.НайтиСтроки(Отбор);
	Для каждого Строка из мНайденныеСтроки Цикл
		стр = СтрЗаменить(Строка.ИмяОбъектаКонфигурации, ".", Символы.ПС);
		
		ИмяЭлемента = СтрПолучитьСтроку(стр, 1);
		НовыйУзел = мДеревоКонфигурацииDOM.СоздатьЭлемент(ИмяЭлемента);
		НовыйУзел.ТекстовоеСодержимое = СтрПолучитьСтроку(стр, 2);
		
		КорневойУзелОбъектов.ДобавитьДочерний(НовыйУзел);
	КонецЦикла;
	
КонецПроцедуры


Процедура СоздатьФайлыСборки()
	УдалитьФайлы(КаталогСборки, "*");
	
	Для каждого Строка из мДеревоКонфигурацииТаблица Цикл
		
		Маска = ?(Строка.ЭтоЗаглушка, "*.xml", ".*");
		
		мФайлы = НайтиФайлы(КаталогИсходныхФайлов, Строка.ИмяОбъектаКонфигурации+Маска);
		Для каждого Файл из мФайлы Цикл
			Если НЕ Строка.ЭтоЗаглушка Тогда
			ИначеЕсли Найти(Файл.Имя, ".Style.xml") Тогда
				Продолжить;
			ИначеЕсли Найти(Файл.Имя, ".Help.xml") Тогда
				Продолжить;
			ИначеЕсли Найти(Файл.Имя, ".Form.xml") Тогда
				Продолжить;
			ИначеЕсли Найти(Файл.Имя, ".Picture.xml") Тогда
				Продолжить;
			ИначеЕсли Найти(Файл.Имя, ".Template.") Тогда
				Продолжить;
			КонецЕсли;
			ИмяФайлаПриемника = КаталогСборки + Файл.Имя;
			КопироватьФайл(Файл.ПолноеИмя, ИмяФайлаПриемника);
		КонецЦикла;
	КонецЦикла;
	
	// загрузка модулей приложения
	мФайлы = НайтиФайлы(КаталогИсходныхФайлов, "Configuration.*.txt");
	Для каждого Файл из мФайлы Цикл
		ИмяФайлаПриемника = КаталогСборки + Файл.Имя;
		КопироватьФайл(Файл.ПолноеИмя, ИмяФайлаПриемника);
	КонецЦикла;
	
	ЗаписатьDOMОбъектВФайл(мДеревоКонфигурацииDOM, КаталогСборки + "Configuration.xml");
	
КонецПроцедуры

Процедура УдалитьРеквизитыВЗаглушках()
	
	ШаблоныУдаления = Новый Массив;
	ШаблоныУдаления.Добавить("Template");
	ШаблоныУдаления.Добавить("v8:Type");
	ШаблоныУдаления.Добавить("Page");
	//ШаблоныУдаления.Добавить("Group");	
	
	ШаблоныОчистки = Новый Массив();
	ШаблоныОчистки.Добавить("Type");
	ШаблоныОчистки.Добавить("DefaultObjectForm");
	ШаблоныОчистки.Добавить("DefaultFolderForm");
	ШаблоныОчистки.Добавить("DefaultListForm");
	ШаблоныОчистки.Добавить("DefaultChoiceForm");
	ШаблоныОчистки.Добавить("DefaultFolderChoiceForm");
	ШаблоныОчистки.Добавить("AuxiliaryObjectForm");
	ШаблоныОчистки.Добавить("AuxiliaryFolderForm");
	ШаблоныОчистки.Добавить("AuxiliaryListForm");
	ШаблоныОчистки.Добавить("AuxiliaryChoiceForm");
	ШаблоныОчистки.Добавить("AuxiliaryFolderChoiceForm");
	ШаблоныОчистки.Добавить("ChoiceForm");
	ШаблоныОчистки.Добавить("InputByString");
	ШаблоныОчистки.Добавить("BasedOn");
	ШаблоныОчистки.Добавить("RegisterRecords");
	ШаблоныОчистки.Добавить("CharacteristicExtValues");
	//ШаблоныОчистки.Добавить("ExtDimensionTypes");
	ШаблоныОчистки.Добавить("Owners");
	ШаблоныОчистки.Добавить("DefaultSaveForm");
	ШаблоныОчистки.Добавить("DefaultLoadForm");	
	ШаблоныОчистки.Добавить("DefaultForm");	
	ШаблоныОчистки.Добавить("MainDataCompositionSchema");	
	ШаблоныОчистки.Добавить("DefaultSettingsForm");	
	ШаблоныОчистки.Добавить("AuxiliaryForm");	
	ШаблоныОчистки.Добавить("DefaultVariantForm");	
	ШаблоныОчистки.Добавить("ChoiceParameters");	
	ШаблоныОчистки.Добавить("FillValue");	
	ШаблоныОчистки.Добавить("Numerator");	
	ШаблоныОчистки.Добавить("DefaultRecordForm");	
	ШаблоныОчистки.Добавить("AuxiliaryRecordForm");	
	ШаблоныОчистки.Добавить("Addressing");	
	ШаблоныОчистки.Добавить("AddressingDimension");	
	ШаблоныОчистки.Добавить("CurrentPerformer");	
	
	Отбор = Новый Структура("ЭтоЗаглушка", Истина);
	мНайденныеСтроки = мДеревоКонфигурацииТаблица.НайтиСтроки(Отбор);
	
	Для каждого Строка из мНайденныеСтроки Цикл
		
		Если Найти(Строка.ИмяОбъектаКонфигурации, "Enum.") = 1 Тогда
			Продолжить;
		ИначеЕсли Найти(Строка.ИмяОбъектаКонфигурации, "StyleItem.") = 1 Тогда
			Продолжить;
		КонецЕсли;
		
		НайденныеФайлы = НайтиФайлы(КаталогСборки, Строка.ИмяОбъектаКонфигурации + ".xml");
		ДополнительныйМассивФайлов = НайтиФайлы(КаталогСборки, Строка.ИмяОбъектаКонфигурации + ".*.xml");
		Для каждого Файл из ДополнительныйМассивФайлов Цикл
			НайденныеФайлы.Добавить(Файл);
		КонецЦикла;
		
		Если НайденныеФайлы.Количество() = 0 Тогда
			Сообщить("Не найден файл: " + Строка.ИмяОбъектаКонфигурации + ".xml");
			Продолжить;
		КонецЕсли;
		
		Для каждого ФайлОбъекта из НайденныеФайлы Цикл
			ИмяФайлаОбъекта = ФайлОбъекта.ПолноеИмя;
			
			ОбъектDOM = ПрочитатьDOMИзФайла(ИмяФайлаОбъекта);
			
			Для каждого Шаблон из ШаблоныУдаления Цикл
				мНайденныеУзлы = ОбъектDOM.ПолучитьЭлементыПоИмени(Шаблон);
				Для сч = -мНайденныеУзлы.Количество() по -1 Цикл
					НайденныйУзел = мНайденныеУзлы[-сч-1];
					РодительскийУзел = НайденныйУзел.РодительскийУзел;
					РодительскийУзел.УдалитьДочерний(НайденныйУзел);
				КонецЦикла;
			КонецЦикла;
			
			Для каждого Шаблон из ШаблоныОчистки Цикл
				мНайденныеУзлы = ОбъектDOM.ПолучитьЭлементыПоИмени(Шаблон);
				Для сч = -мНайденныеУзлы.Количество() по -1 Цикл
					НайденныйУзел = мНайденныеУзлы[-сч-1];
					
					Если НЕ мДеревоКонфигурацииТаблица.Найти(НайденныйУзел.ТекстовоеСодержимое, "ИмяОбъектаКонфигурации") = Неопределено Тогда
						Продолжить;
					КонецЕсли;
					
					КоличествоУзлов = НайденныйУзел.ДочерниеУзлы.Количество();
					Для счУдаляемыеУзлы = -КоличествоУзлов по -1 Цикл
						УдаляемыйУзел = НайденныйУзел.ДочерниеУзлы[-счУдаляемыеУзлы-1];
						Если НайденныйУзел.ИмяЭлемента = "Owners" 
								И НЕ мДеревоКонфигурацииТаблица.Найти(УдаляемыйУзел.ТекстовоеСодержимое, "ИмяОбъектаКонфигурации") = Неопределено Тогда
							Продолжить;
						КонецЕсли;
						НайденныйУзел.УдалитьДочерний(УдаляемыйУзел);
					КонецЦикла;
					Если ПустаяСтрока(НайденныйУзел.Префикс) И НЕ НайденныйУзел.ЕстьДочерниеУзлы() Тогда
						НовыйУзел = НайденныйУзел.КлонироватьУзел(Ложь);
						РодительскийУзел = НайденныйУзел.РодительскийУзел;
						РодительскийУзел.ЗаменитьДочерний(НовыйУзел, НайденныйУзел);
					КонецЕсли;
					
				КонецЦикла;
			КонецЦикла;
			
			ЗаписатьDOMОбъектВФайл(ОбъектDOM, ИмяФайлаОбъекта);
		
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьПодсистемы()
	
	ИмяПодсистемы = "ПереносимыеОбъекты";
	Отбор = Новый Структура("ЭтоЗаглушка", Ложь);
	НайденныеСтроки = мДеревоКонфигурацииТаблица.НайтиСтроки(Отбор);
	СоздатьФайлПодсистемы(ИмяПодсистемы, НайденныеСтроки);
	ДобавитьПодсистемуВРеестр(ИмяПодсистемы);
	
	ИмяПодсистемы = "Заглушки";
	Отбор = Новый Структура("ЭтоЗаглушка", Истина);
	НайденныеСтроки = мДеревоКонфигурацииТаблица.НайтиСтроки(Отбор);
	СоздатьФайлПодсистемы(ИмяПодсистемы, НайденныеСтроки);
	ДобавитьПодсистемуВРеестр(ИмяПодсистемы);
	
КонецПроцедуры
Процедура СоздатьФайлПодсистемы(ИмяПодсистемы, СтрокиДереваКонфигурации)
	Макет = ПолучитьМакет("ШаблонПодсистемы");
	ОбъектDOM = ПрочитатьDOMИзСтроки(Макет.ПолучитьТекст());
	
	ОбъектDOM.ПолучитьЭлементыПоИмени("Subsystem")[0].УстановитьАтрибут("uuid", Строка(Новый УникальныйИдентификатор()));
	ОбъектDOM.ПолучитьЭлементыПоИмени("Name")[0].ТекстовоеСодержимое = ИмяПодсистемы;
	
	РодительскийУзел = ОбъектDOM.ПолучитьЭлементыПоИмени("Content")[0];
	Узел_Состав = РодительскийУзел.ДочерниеУзлы[0];
	Для каждого Строка из СтрокиДереваКонфигурации Цикл
		НовыйУзел = Узел_Состав.КлонироватьУзел(Истина);
		РодительскийУзел.ВставитьПеред(НовыйУзел, Узел_Состав);
		НовыйУзел.ТекстовоеСодержимое = Строка.ИмяОбъектаКонфигурации;
	КонецЦикла;
	
	РодительскийУзел.УдалитьДочерний(Узел_Состав);
	
	ПутьФайлаПодсистемы = КаталогСборки + "Subsystem."+ИмяПодсистемы+".xml";
	ЗаписатьDOMОбъектВФайл(ОбъектDOM, ПутьФайлаПодсистемы);
	
КонецПроцедуры
Процедура ДобавитьПодсистемуВРеестр(ИмяПодсистемы)
	ИмяЭлемента = "Subsystem";
	НовыйУзел = мДеревоКонфигурацииDOM.СоздатьЭлемент(ИмяЭлемента);
	НовыйУзел.ТекстовоеСодержимое = ИмяПодсистемы;
	КорневойУзелОбъектов = мДеревоКонфигурацииDOM.ПолучитьЭлементыПоИмени("ChildObjects")[0];
	КорневойУзелОбъектов.ДобавитьДочерний(НовыйУзел);
	
	ПутьКФайлуДереваКонфигурации = КаталогСборки + "Configuration.xml";
	ЗаписатьDOMОбъектВФайл(мДеревоКонфигурацииDOM, ПутьКФайлуДереваКонфигурации);
	
КонецПроцедуры

//{ Работа с DOM

Функция ПрочитатьDOMИзФайла(ПутьКФайлу) 
	
	ЧтениеXML=Новый ЧтениеXML(); 
	ПараметрыЧтения = Новый ПараметрыЧтенияXML(,,,ТипПроверкиXML.НетПроверки);
	ЧтениеXML.ОткрытьФайл(ПутьКФайлу, ПараметрыЧтения);
	ПостроительDOM = Новый ПостроительDOM;
	ОбъектDOM = ПостроительDOM.Прочитать(ЧтениеXML);
	ЧтениеXML.Закрыть();
	
	Возврат ОбъектDOM;
	
КонецФункции
Процедура ОчиститьДочерниеУзлыDOMОбъекта(DOMОбъект, ИмяРодительскогоУзла)
	
	НайденныйУзел = DOMОбъект.ПолучитьЭлементыПоИмени(ИмяРодительскогоУзла)[0];
	Пока НайденныйУзел.ЕстьДочерниеУзлы() Цикл
		НайденныйУзел.УдалитьДочерний(НайденныйУзел.ПервыйДочерний);
	КонецЦикла;
	
КонецПроцедуры
Процедура ЗаписатьDOMОбъектВФайл(DOMОбъект, ПутьКФайлу)
	
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.ОткрытьФайл(ПутьКФайлу);
	ЗаписьDOM = Новый ЗаписьDOM();
	ЗаписьDOM.Записать(DOMОбъект, ЗаписьXML);
	ЗаписьXML.Закрыть();	
	
КонецПроцедуры
Функция ПрочитатьDOMИзСтроки(Строка)
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(Строка);
	ПостроительDOM = Новый ПостроительDOM;
	
	Возврат ПостроительDOM.Прочитать(ЧтениеXML);
КонецФункции

//}

//{ Работа с файлами

Функция ПутьККопииФайла(ПутьКФайлу)
	Файл = Новый Файл(ПутьКФайлу);
	НовыйПутьКФайлу = КаталогВременныхФайлов() + Файл.Имя;
	
	КопироватьФайл(ПутьКФайлу, НовыйПутьКФайлу);
	
	Возврат НовыйПутьКФайлу;
КонецФункции
Функция ПутьККаталогуСоСлешем(ПутьККаталогуСоСлешемИлиБез)
	
	Файл = Новый Файл(ПутьККаталогуСоСлешемИлиБез);
	Возврат Файл.ПолноеИмя + ПолучитьРазделительПути();
	
КонецФункции
Функция ПолучитьРасширениеФайла(ПутьКФайлу)
	Файл = Новый Файл(ПутьКФайлу);
	Возврат Файл.Расширение;
КонецФункции

//}